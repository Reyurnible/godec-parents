apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.1"

    defaultConfig {
        applicationId "com.hosshan.android.kodic"
        versionCode 1
        versionName "1.0"
        minSdkVersion 15
        targetSdkVersion 23
        renderscriptTargetApi 23
        renderscriptSupportModeEnabled true
    }
    signingConfigs {
        debug {
            storeFile file("key/debug.jks")
            storePassword "debug1234"
            keyAlias "kodic-debug"
            keyPassword "debug1234"
        }
        release {
            storeFile file("key/release.jks")
            storePassword "release1234"
            keyAlias "kodic-release"
            keyPassword "release1234"
        }
    }
    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        debug.java.srcDirs += 'src/debug/kotlin'
        release.java.srcDirs += 'src/release/kotlin'
    }
    lintOptions {
        abortOnError false
    }
}

def rxBindingVersion = '0.3.0'
def supportLibVersion = '23.1.0'

/*
 * ## Sorting
 * sorting dependencies by library group
 * Main is BuildVariants order
 * Sub is Alphabet order
 *
 * ### Variants Order
 * 1. Compile
 * 2. Debug
 * 3. Release
 * 4. Flavor
 * 5. Test
 *
 * ### Alphabet Order
 * A..Z
 */
dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile project(':guillotine-menu')
    // Android Support Library
    compile "com.android.support:support-v4:$supportLibVersion"
    compile "com.android.support:appcompat-v7:$supportLibVersion"
    compile "com.android.support:cardview-v7:$supportLibVersion"
    compile "com.android.support:recyclerview-v7:$supportLibVersion"
    compile "com.android.support:design:$supportLibVersion"
    // Dagger
    compile 'com.google.dagger:dagger:2.0.1'
    kapt 'com.google.dagger:dagger-compiler:2.0.1'
    provided 'org.glassfish:javax.annotation:10.0-b28'
    // Flowing Drawer
    compile 'com.mxn.soul:flowingdrawer-core:1.2.0'
    // Glide
    compile 'com.github.bumptech.glide:glide:3.6.1'
    compile 'com.github.bumptech.glide:okhttp-integration:1.3.1'
    compile 'jp.wasabeef:glide-transformations:1.2.1'
    compile 'jp.co.cyberagent.android.gpuimage:gpuimage-library:1.3.0'
    // Hawk
    compile 'com.github.orhanobut:hawk:1.19'
    // Kotlin
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile 'com.jakewharton:kotterknife:0.1.0-SNAPSHOT'
    // Logger
    compile 'com.jakewharton.timber:timber:3.1.+'
    // Otto
    compile 'com.squareup:otto:1.3.8'
    // Reactive
    compile 'io.reactivex:rxjava:1.0.14'
    compile 'com.cookpad.android.rxt4a:rxt4a:0.10.0'
    compile "com.jakewharton.rxbinding:rxbinding:$rxBindingVersion"
    compile "com.jakewharton.rxbinding:rxbinding-support-v4:$rxBindingVersion"
    compile "com.jakewharton.rxbinding:rxbinding-appcompat-v7:$rxBindingVersion"
    compile "com.jakewharton.rxbinding:rxbinding-recyclerview-v7:$rxBindingVersion"
    compile 'com.f2prateek.rx.preferences:rx-preferences:1.0.0'
    // Realm
    compile 'io.realm:realm-android:0.84.0'
    kapt 'io.realm:realm-android:0.84.0'
    // Retrofit
    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.0.0'
    compile 'com.squareup.okhttp:okhttp:2.5.0'
    testCompile 'junit:junit:4.12'
    compile project(':guillotine-menu')
}

kapt {
    generateStubs = true
}

repositories {
    mavenCentral()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
    maven { url "https://jitpack.io" }
}
